// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Script.sol";
import "solmate/tokens/WETH.sol";
import "nugu/NuguFactory.sol";

import "../contracts/PortfolioRegistry.sol";
import "../contracts/Portfolio.sol";
import "../contracts/PositionRenderer.sol";

// This script prepares all the transaction data that must be used to deploy
// Portfolio and its dependency contracts.
//
// Here are the steps to use it:
// 1. Be sure to update all the constants below: contract addresses and salts.
// 2. Run the script with:
// `forge script ./scripts/Prepare.s.sol --via-ir --optimizer-runs 0`.
//
// If you want to deploy from a Safe Wallet, you can follow the steps below:
// 3. Visit https://app.safe.global/, click on your multisig wallet, then on
// "New transaction" and finally "Transaction builder".
// 4. Paste the address of the Nugu factory and enable the "custom data".
// 5. Then for each data generated by this script (3 in total), you'll need to
// copy and paste it into the "data" field of the transaction builder and click
// the "Add transaction" button.
// 6. After that, a total of 3 transactions must compose the batch. You can then
// hit "Create Batch", and either "Simulate" or directly "Send Batch".
// 7. Last step is simply to follow the instructions to send the transaction.
//
// If you want to deploy from a hardware wallet, you can follow the steps below:
// 3. Visit any website allowing you to send a transaction with custom data (such
// as https://www.myetherwallet.com/).
// 4. Paste the address of the Nugu factory as a recipient, paste the first custom
// data that was generated and send the transaction.
// 5. Repeat step 4 for all the data generated by this script (3 in total).

address constant MULTISIG_WALLET = 0xD86872c94484abf03EA8F98ED8E91E9D943781CE;
address constant NUGU_ADDRESS = 0xe50ea0e9849cb17829907Acaf764af8F37d4938E;
address constant WETH_ADDRESS = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;

bytes32 constant REGISTRY_SALT = keccak256("REGISTRY0");
bytes32 constant POSITION_RENDERER_SALT = keccak256("POSITION_RENDERER0");
bytes32 constant PORTFOLIO_SALT =
    bytes32(0x0EB4AB9B29D4F1CFBCDB500D936BCD61BC941EFDBCD3141738F68F972CC15ED4);

contract Deploy is Script {
    bytes internal constant PROXY_BYTECODE =
        hex"67363d3d37363d34f03d5260086018f3";
    bytes32 internal constant PROXY_BYTECODE_HASH = keccak256(PROXY_BYTECODE);

    function run() external {
        bytes memory registryDeployData = abi.encodeCall(
            NuguFactory.deploy,
            (
                REGISTRY_SALT,
                abi.encodePacked(
                    type(PortfolioRegistry).creationCode,
                    abi.encode(MULTISIG_WALLET)
                    )
            )
        );

        bytes memory positionRendererDeployData = abi.encodeCall(
            NuguFactory.deploy,
            (POSITION_RENDERER_SALT, type(PositionRenderer).creationCode)
        );

        address predictedRegistryAddress =
            getDeployed(NUGU_ADDRESS, MULTISIG_WALLET, REGISTRY_SALT);

        address predictedPositionRendererAddress =
            getDeployed(NUGU_ADDRESS, MULTISIG_WALLET, POSITION_RENDERER_SALT);

        bytes memory portfolioDeployData = abi.encodeCall(
            NuguFactory.deploy,
            (
                PORTFOLIO_SALT,
                abi.encodePacked(
                    type(Portfolio).creationCode,
                    abi.encode(
                        WETH_ADDRESS,
                        predictedRegistryAddress,
                        predictedPositionRendererAddress
                    )
                    )
            )
        );

        console.log("\nRegistry deploy data:");
        console.logBytes(registryDeployData);

        console.log("\nPositionRenderer deploy data:");
        console.logBytes(positionRendererDeployData);

        console.log("\nPortfolio deploy data:");
        console.logBytes(portfolioDeployData);
        console.log("\n Portfolio address:");
        console.log(getDeployed(NUGU_ADDRESS, MULTISIG_WALLET, PORTFOLIO_SALT));
    }

    function getDeployed(
        address factory,
        address deployer,
        bytes32 salt
    ) internal view returns (address) {
        salt = keccak256(abi.encodePacked(deployer, salt));

        address proxy = Bytes32AddressLib.fromLast20Bytes(
            keccak256(
                abi.encodePacked(
                    bytes1(0xFF), factory, salt, PROXY_BYTECODE_HASH
                )
            )
        );

        return Bytes32AddressLib.fromLast20Bytes(
            keccak256(abi.encodePacked(hex"d694", proxy, hex"01"))
        );
    }
}
